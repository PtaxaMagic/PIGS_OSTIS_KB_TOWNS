scp_program -> proc_add_outline_to_answer
(*
	-> rrel_params: ...
	(*
		-> rrel_1: rrel_in: _outline;;
		-> rrel_2: rrel_in: _answer;;	
	*);;

	-> rrel_operators: ... 
	(*
		-> rrel_init: .proc_add_outline_to_answer_operator1
		(*
			<- searchSetStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _outline;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_of_vertexs1;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _vert;;

			-> rrel_set_3: rrel_assign: rrel_scp_var: _elements;;
			
			=> nrel_then: .proc_add_outline_to_answer_operator2;;
                	=> nrel_else: .proc_add_outline_to_answer_operator8;;
		*);;

		-> .proc_add_outline_to_answer_operator2
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _elements;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc_erase;;
                        -> rrel_3: rrel_assign: rrel_scp_var: _element;;
			
			=>nrel_then: .proc_add_outline_to_answer_operator3;;
			=>nrel_else: .proc_add_outline_to_answer_operator7;; 	
		*);;

		-> .proc_add_outline_to_answer_operator3
		(*
			<- searchElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _outline;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _element;;

			=>nrel_goto: .proc_add_outline_to_answer_operator4;; 	
		*);;		

		-> .proc_add_outline_to_answer_operator4
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arca;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _arc1;;
			
			=>nrel_goto: .proc_add_outline_to_answer_operator5;; 	
		*);;

		-> .proc_add_outline_to_answer_operator5
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _element;;
			
			=> nrel_goto: .proc_add_outline_to_answer_operator6;; 	
		*);;

		-> .proc_add_outline_to_answer_operator6
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _arc_erase;;
			
			=> nrel_goto: .proc_add_outline_to_answer_operator2;;
		*);;

		-> .proc_add_outline_to_answer_operator7
		(*
			<- eraseEl;;
			-> rrel_1: rrel_fixed: rrel_scp_var: rrel_erase: _elements;;
			
			=> nrel_goto: .proc_add_outline_to_answer_operator8;;
		*);;

		-> .proc_add_outline_to_answer_operator8
		(*
			<- genElStr3;;
			-> rrel_1: rrel_fixed: rrel_scp_var: _answer;;
                        -> rrel_2: rrel_assign: rrel_scp_var: rrel_pos_const_perm: _arc1;;
                        -> rrel_3: rrel_fixed: rrel_scp_var: _outline;;
			
			=> nrel_goto: .proc_add_outline_to_answer_return;; 	
		*);;

		-> .proc_add_outline_to_answer_return(*<-return;;*);;
	*);;
*);;

